
; Speaker placement
;      1  
; 2          8
; 3          7
; 4          6
;      5

; Each day gets its own MIDI channel.
; The "intro" to each pass gets its own MIDI channel.
; The final "in a pear tree" is played on all channels.
;
;  0 - Intro
;  1 - One
;  2 - Two
;  3 - Three
;  4 - Four
;  5 - Five
;  6 - Six
;  7 - Seven
;  8 - Eight
; Channel 9 is reserved by the hardware for drums.
; 10 - Nine
; 11 - Ten
; 12 - Eleven
; 13 - Twelve

; -------- Music fragments. These get distributed into the tracks later. --------

Gave_Rest%
  4R     | 1R                 | 1R                  |
FirstGave_Rest%
  4R     | 1R                 | 1R                  |
FirstGave_Notes%
; On the 1st day of Christmas ... gave to me
  8D4 D  |  4D 8G G 4G 8F# G  |  A B C+ A- 4.B  8C+ |
^ 4R     |  2B3 D+            |  F#    D            |
^ 4R     |  2G2 4G 4A         |  2D+   2G           |
^ 4R     |  2R  2B3           |  D+    B-           |
Gave_Notes%
; On the 1st day of Christmas ... gave to me
  8D4 D  |  4D 8G G 4G 8F# G  |  A B C+ A- 4.B  8R  |
^ 4R     |  2B3 D+            |  F#    D            |
^  R     |  2G2 4G 4A         |  2D+   2G           |
^ 4R     |  2R  2B3           |  D+    B-           |

One_Rest%
  1R                     | 2.R  |
One_Notes%
  4D5 8E 8C 8B- 8G 4A    | 2.G  |
^ 4D5 8E C B- G 4A       | 2.G  |
^ 4B4 8C+ 8R 8G- 8R 4F#  | 2.D  |
^ 4G4 8G 8R 8D 8R 4C     | 2.B- |
^ 4G3 4C 4G 4D           | 2.G  |

; Before and After
;
; Days two, three, and four have two different forms. One form is played
; BEFORE you get to day five. AFTER day five the other form is always
; used.

Before_Two_Rest%
  1R                     |
After_Two_Rest%
  1R                     |
Before_Two_Notes%
  4D5 8A- 8B 4C+ 8B- 8C+ |
^ 1D4                    |
^ 1A3                    |
After_Two_Notes%
  8A4 G F# E 4D 8B C+    |
^ 2C4# 2A-               |
^ 2A3 2F3#               |
^ 2E3 2D                 |
^ 2A2 2R                 |

Before_Three_Rest%
  2.R              |
After_Three_Rest%
  2.R              |
Before_Three_Notes%
  4D5 8A- B 4C+    |
^ 2.F4#            |
^ 2.D4             |
^ 2.A3             |
After_Three_Notes%
  4C5 E- G         |
^ 4G4 C E          |
^ 4E4 2R           |
^ 2C3 4C           |

Before_Four_Rest%
  2.R            |
After_Four_Rest%
  2.R            |
Before_Four_Notes%
  4D5 8A- B 4C+  |
^ 2.F4#          |
^ 2.D4           |
^ 2.A3           |
After_Four_Notes%
  8D5 C B- A 4G  |
^ 2G4 4D         |
^ 2D4 4B-        |
^ 2B3 4B-        |
^ 2G3 4R         |

Five_Rest%
  1R        | 1R       |
Five_Notes%
  2D5 4E C# | 2.D 4R   |
^ 2D5 4E C# | 1D       |
^ 2B4 4G G  | 1F#      |
^ 2G4 4R D  | 2.A3 4C+ |
^ 2G3 A     | 1D       |

; Six through Twelve are all the same. We still need separate names so
; we can point the different days to different speakers.

SixToTwelve_Rest%
  2.R             |
SixToTwelve_Notes%
  4D5 8A- B C+ A- |
^ 2.F4#           |
^ 2.D4            |
^ 4D5 8A- B 4C+   |
^ 2.A3            |
^ 2.D3            |

Six_Rest%
  %SixToTwelve_Rest
Six_Notes%
  %SixToTwelve_Notes
  
Seven_Rest%
  %SixToTwelve_Rest
Seven_Notes%
  %SixToTwelve_Notes
  
Eight_Rest%
  %SixToTwelve_Rest
Eight_Notes%
  %SixToTwelve_Notes
  
Nine_Rest%
  %SixToTwelve_Rest
Nine_Notes%
  %SixToTwelve_Notes
  
Ten_Rest%
  %SixToTwelve_Rest
Ten_Notes%
  %SixToTwelve_Notes
  
Eleven_Rest%
  %SixToTwelve_Rest
Eleven_Notes%
  %SixToTwelve_Notes
  
Twelve_Rest%
  %SixToTwelve_Rest
Twelve_Notes%
  %SixToTwelve_Notes
  
;-------- Putting the fragments into sequences --------

;---- Just the final pass
;
Sequence%
;
%Gave_Rest
%Twelve_Rest
%Eleven_Rest
%Ten_Rest
%Nine_Rest
%Eight_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest

;---- The full song
;
FullSequence%
;
%FirstGave_Rest     
%One_Rest           
;
%Gave_Rest          
%Before_Two_Rest    
%One_Rest           
;
%Gave_Rest          
%Before_Three_Rest  
%Before_Two_Rest
%One_Rest
;
%Gave_Rest
%Before_Four_Rest
%Before_Three_Rest
%Before_Two_Rest
%One_Rest
;
%Gave_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Eight_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Nine_Rest
%Eight_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Ten_Rest
%Nine_Rest
%Eight_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Eleven_Rest
%Ten_Rest
%Nine_Rest
%Eight_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest
;
%Gave_Rest
%Twelve_Rest
%Eleven_Rest
%Ten_Rest
%Nine_Rest
%Eight_Rest
%Seven_Rest
%Six_Rest
%Five_Rest
%After_Four_Rest
%After_Three_Rest
%After_Two_Rest
%One_Rest

; --- TESTING
TestSequence%
%Gave_Rest

; -------- Here is the actual mapping of fragments to tracks -----------
; The values in parenthesis are direct substitutions into the given
; sequence.
;
; Be sure to assign channels to speakers and sounds to channels.

Track 0: ; Intro ---------------------------------------------------
Patch_2 ; Bright Acoustic Piano
%Sequence(Gave_Rest   <-Gave_Notes)

Track 1: ; One ---------------------------------------------------
Patch_70 ; English Horn
%Sequence(One_Rest    <-One_Notes)

Track 2: ; Two ---------------------------------------------------
Patch_74 ; Flute
%Sequence(Two_Rest    <-Two_Notes)

Track 3: ; Three ---------------------------------------------------
Patch_57 ; Trumpet
%Sequence(Three_Rest  <-Three_Notes)

Track 4: ; Four ---------------------------------------------------
Patch_68 ; Baratone Sax
%Sequence(Four_Rest   <-Four_Notes)

Track 5: ; Five  ---------------------------------------------------
Patch_1 ; Grand Piano
%Sequence(Five_Rest   <-Five_Notes)

Track 6: ; Six ---------------------------------------------------
Patch_5 ; Electric Piano
%Sequence(Six_Rest    <-Six_Notes)

Track 7: ; Seven ---------------------------------------------------
Patch_22 ; Accordion
%Sequence(Seven_Rest  <-Seven_Notes)
          
Track 8: ; Eight ---------------------------------------------------
Patch_20 ; Church Organ
%Sequence(Eight_Rest  <-Eight_Notes)

Track 10: ; Nine ---------------------------------------------------
Patch_72 ; Clarinet
%Sequence(Nine_Rest   <-Nine_Notes)
          
Track 11: ; Ten ---------------------------------------------------
Patch_75 ; Recorder
%Sequence(Ten_Rest    <-Ten_Notes)
          
Track 12: ; Eleven ---------------------------------------------------
Patch_65 ; Soprano Sax
%Sequence(Eleven_Rest <-Eleven_Notes)
          
Track 13: ; Twelve ---------------------------------------------------
Patch_26 ; Acoustic Guitar (steel)
%Sequence(Twelve_Rest <-Twelve_Notes)
